Sub makeInvoice()
    
  'CHOOSE FOLDER TO SAVE FILES
    Dim folderPath As String
    With Application.FileDialog(msoFileDialogFolderPicker)
        If .Show = -1 Then
            folderPath = .SelectedItems(1)
        Else
            Exit Sub
        End If
    End With
    
    Dim invTempSheet As Worksheet
    Set invTempSheet = ThisWorkbook.Sheets("INV TEMPLATE")
    Dim invListSheet As Worksheet
    Set invListSheet = ThisWorkbook.Sheets("INV list")
    
    Dim lastMerchant As Integer
    lastMerchant = invListSheet.Range("A" & invListSheet.Rows.Count).End(xlUp).Row

    Dim i As Integer
    For i = 3 To lastMerchant
    
  'SET MERCHANT
        invTempSheet.Activate
        invTempSheet.Range("A12").Value = invListSheet.Range("B" & i).Value
       
  'DUPLICATE SHEET
        Dim lastSheet As Worksheet
        Set lastSheet = ThisWorkbook.Sheets(ThisWorkbook.Sheets.Count)
        
        invTempSheet.Copy After:=lastSheet
        
        Dim newSheet As Worksheet
        Set newSheet = ActiveSheet
        newSheet.Name = invListSheet.Range("A" & i).Value
   
  'CONVERT FORMULAS TO VALUES
        newSheet.Range("H7:H8").Value = newSheet.Range("H7:H8").Value
        newSheet.Range("C16:E17").Value = newSheet.Range("C16:E17").Value
        newSheet.Range("A44").Value = newSheet.Range("A44").Value
        newSheet.Range("A47:H77").Copy
        newSheet.Range("A47:H77").PasteSpecial xlPasteValues
        Application.CutCopyMode = False
        newSheet.Range("A44").Select
        
  'DELETE BLANKS
        
        Dim startRow As Integer
        startRow = newSheet.Range("B80").End(xlUp).Row + 2
        
        If startRow <= 78 Then
            newSheet.Rows(startRow & ":" & 78).Delete
        End If

  'PRINT
        Dim file_Name As String
        file_Name = invListSheet.Range("C" & i).Value & ".pdf"
        newSheet.ExportAsFixedFormat Type:=xlTypePDF, _
                                     Filename:=folderPath & "\" & file_Name, _
                                     Quality:=xlQualityStandard, _
                                     IncludeDocProperties:=True, _
                                     IgnorePrintAreas:=False, _
                                     OpenAfterPublish:=False
    Next i
End Sub



